apply plugin: 'com.android.application'

ext.versionMajor = 0
ext.versionMinor = 10
ext.versionPatch = 4
ext.versionClassifier = "beta" // or null
ext.isSnapshot = false // set to false when publishing new releases
ext.minimumSdkVersion = 23
ext.targetSdkVersion = 31

private Integer GenerateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String GenerateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }

    if (ext.isSnapshot) {
        versionName += "-" + "SNAPSHOT"
    }
    return versionName;
}

android {
    namespace = "io.github.trojan_gfw.igniter"
    ndkVersion "23.1.7779620"
    compileSdk targetSdkVersion

    applicationVariants.configureEach { variant ->
        variant.resValue "string", "versionName", variant.versionName
    }

    defaultConfig {
        applicationId "io.github.trojan_gfw.igniter"
        minSdkVersion project.ext.minimumSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode GenerateVersionCode()
        versionName GenerateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
        externalNativeBuild {
            cmake {
                arguments "-DANDROID_CPP_FEATURES=rtti exceptions"
            }
        }
        archivesBaseName = "$applicationId-v$versionName-$versionCode"
    }


    buildFeatures {
        aidl = true
        renderScript = true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }
    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
    }
    packagingOptions {
        doNotStrip "*/armeabi-v7a/libgojni.so"
        doNotStrip "*/arm64-v8a/libgojni.so"
        doNotStrip "*/x86/libgojni.so"
        doNotStrip "*/x86_64/libgojni.so"
        jniLibs {
            // During installation, the installer decompresses the libraries, and the linker loads
            // the decompressed libraries at runtime; in this case, the APK would be smaller, but
            // installation time might be slightly longer.
            // We optimize for size to make users happy
            useLegacyPackaging = true
        }
    }
    splits {
        abi {
            enable true
            reset()
            include "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            universalApk true
        }
    }
}

repositories {
    flatDir {
        dirs 'src/libs'
    }
}

dependencies {
    implementation 'com.github.stealthcopter:AndroidNetworkTools:0.4.5.3'
    implementation 'com.google.android.material:material:1.9.0-beta01'
    implementation 'androidx.activity:activity:1.7.0'
    implementation 'androidx.fragment:fragment:1.5.6'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core:1.12.0-alpha01'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'com.google.code.gson:gson:2.8.9'
    // CameraX core library using camera2 implementation
    implementation 'androidx.camera:camera-camera2:1.3.0-alpha05'
    // CameraX Lifecycle Library
    implementation 'androidx.camera:camera-lifecycle:1.3.0-alpha05'
    // CameraX View class
    implementation 'androidx.camera:camera-view:1.3.0-alpha05'
    implementation 'com.google.mlkit:barcode-scanning:17.1.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    api(name: 'golibs', ext: 'aar')
}
